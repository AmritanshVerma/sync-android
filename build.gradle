apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'

/* This adds the gradle cobertura plugin */
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:2.1.0"
    }
}
apply plugin: 'cobertura'
/* End of cobertura plugin */


// Allow developer-local configuration to be applied (e.g., customising
// uploadArchives for local maven repo).
def localGradleScript = file("local.gradle")
if (localGradleScript.exists()) {
    apply from: localGradleScript
}

group = 'com.cloudant'
version = '0.3.0'

description = """cloudant-sync"""

sourceCompatibility = 1.6
targetCompatibility = 1.6

defaultTasks 'build'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://cloudant.github.io/cloudant-sync-eap/repository/" }
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.1.1'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version:'4.2.3'
    compile group: 'com.almworks.sqlite4java', name: 'sqlite4java', version:'0.282'
    compile group: 'com.google.guava', name: 'guava', version:'15.0'
    compile group: 'commons-io', name: 'commons-io', version:'2.4'
    compile group: 'com.google.android', name: 'android', version:'4.0.1.2'

    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version:'1.3'
    testCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.9.5'
}

javadoc {
    excludes = ["**/*sql*/**", "**/common/**", "**/android/**"]
    options.showFromPublic()
}

tasks.withType(Test) {
    systemProperty "sqlite4java.library.path", "native"
    systemProperty "file.encoding", "UTF-8"
}

test {
    useJUnit {
        excludeCategories 'com.cloudant.common.SystemTest', 'com.cloudant.common.RequireRunningCouchDB', 'com.cloudant.common.PerformanceTest'
    }
}

task integrationTest(type: Test, dependsOn: testClasses) {
    useJUnit {
        excludeCategories 'com.cloudant.common.SystemTest', 'com.cloudant.common.PerformanceTest'
    }
}

task performanceTest(type: Test, dependsOn: testClasses) {
    useJUnit {
        includeCategories 'com.cloudant.common.PerformanceTest'
    }
}

task systemTest(type: Test, dependsOn: testClasses) {
    // Run all tests
}

task listJars << {
    configurations.testCompile.each { File file -> println file.name }
}

